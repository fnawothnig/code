#!/usr/bin/env perl
use warnings;
use strict;
use Getopt::Std;

my %opts;

sub usage {
	print STDERR for
	"Usage: urlencode [-Adprx] [string]\n",
	"\n",
	"    -A    encode for mq\n",
	"    -d    decode\n",
	"    -o    oct-encode (\\123)\n",
	"    -p    encode full path (allowing slashes)\n",
	"    -r    do not print newline\n",
	"    -x    hex-encode (\\xAB)\n";
	exit;
}

sub decode {
	if ($opts{A}) { s/^:// }
	s/%([A-Fa-f0-9]{2})/pack('C', hex($1))/seg;
}

sub encode {
	if ($opts{A}) {
		s/[\x00-\x1F %]/sprintf("%%%02X", ord($&))/seg;
		s/^$|^:/:$&/;
	} elsif ($opts{x}) {
		s/[^A-Za-z0-9_. \/-]/sprintf("\\x%02X", ord($&))/seg;
	} elsif ($opts{o}) {
		s/[^A-Za-z0-9_. \/-]/sprintf("\\%03o", ord($&))/seg;
	} elsif ($opts{p}) {
		s/[^\/A-Za-z0-9_.!~*'-]/sprintf("%%%02X", ord($&))/seg;
	} else {
		s/[^A-Za-z0-9_.!~*'-]/sprintf("%%%02X", ord($&))/seg;
	}
}

sub do_things {
	if ($opts{d}) {
		decode;
	} else {
		encode;
	}
	print;
	print "\n" unless $opts{r};
}

getopts('Adoprx', \%opts) || usage;

if (scalar @ARGV) {
	do_things for @ARGV;
} else {
	while (<STDIN>) {
		chomp unless $opts{r};
		do_things;
	}
}
