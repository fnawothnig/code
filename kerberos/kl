#!/usr/bin/env perl
use warnings;
use strict;
use feature qw(say switch);
no if $] >= 5.017011, warnings => qw(experimental::smartmatch);
use Getopt::Std;

my $counter;

sub usage {
	say for
		"Usage: kl [-c <ccname> | -l]",
		"",
		"\t-c       show contents of given ccache",
		"\t-l       list known ccaches",
		;
	exit 2;
}

sub uniq { my %seen; grep {!$seen{$_}++} @_; }

sub interval {
	my $end = shift;
	my $start = shift // time;
	my ($dif, $s, $m, $h, $d);

	$dif = $end - $start;
	$dif -= $s = $dif % 60; $dif /= 60;
	$dif -= $m = $dif % 60; $dif /= 60;
	$dif -= $h = $dif % 24; $dif /= 24;
	$d = $dif + 0;

	if ($d > 1)	{ "${d}d ${h}h" }
	elsif ($h > 0)	{ "${h}h ${m}m" }
	elsif ($m > 1)	{ "${m} mins" }
	elsif ($s > 45)	{ "a minute" }
	else		{ "${s} secs" }
}

sub mangle_name {
	mangle_principal((shift)->{server_name});
}

sub mangle_principal {
	join "/", map {join ".", reverse split /\./, $_} split /\//, lc shift;
}

sub enum_ccaches {
	my @ccaches;

	open(my $proc, "-|", "pklist", "-l")
		or die "'pklist' not found\n";
	while (<$proc>) {
		my @l = split(/\t/, $_);
		for (shift @l) {
			when ("cache") {
				my ($ccache, $princ) = @l;
				push @ccaches, [$ccache, $princ];
			}
		}
	}
	close($proc);

	return @ccaches;
}

sub display_ccache {
	my ($ccache) = @_;

	my @pklist_args;
	my @fields;
	my $cache;
	my $defprinc;
	my $defrealm;
	my @extrealms;
	my %tgtcreds;
	my %creds;
	my $init;
	my $tgt;

	# read tickets from ccache

	if (defined $ccache) {
		push @pklist_args, ("-c", $ccache);
	}
	open(my $proc, "-|", "pklist", @pklist_args)
		or die "Please install 'pklist' to use this tool.\n";

	while (<$proc>) {
		chomp;
		my @l = split(/\t/, $_);
		for (shift @l) {
			when ("cache") {
				($cache, $defprinc) = @l;
				# If I ever decide to merge the 'cache' line formats in pklist.
				if (defined $defprinc) {
					($defrealm) = $defprinc =~ /@([^@]+)$/;
				}
			}
			when ("principal") {
				($defprinc) = @l;
				($defrealm) = $defprinc =~ /@([^@]+)$/;
			}
			when ("CREDENTIALS") {
				@fields = @l;
			}
			when ("ticket") {
				my %tkt = ();
				@tkt{@fields} = @l;
				my ($name, $realm) = $tkt{server_name} =~ /^(.+)@([^@]*)$/;
				push @extrealms, $realm
					unless $realm eq $defrealm;

				if ($tkt{flags} =~ /I/) {
					$init = \%tkt;
				}

				if ($name =~ m|^krbtgt/(.+)| && $realm ne '') {
					push @{$tgtcreds{$1}}, \%tkt;
					push @extrealms, $1
						unless $1 eq $defrealm;
					if ($1 eq $realm) {
						$tgt = \%tkt;
					}
				} else {
					push @{$creds{$realm}}, \%tkt;
				}
			}
		}
	}
	close($proc);

	if (!defined $cache) {
		return 1;
	}

	if ($defrealm eq 'WELLKNOWN:ANONYMOUS') {
		($defrealm) = ($init // $tgt)->{server_name} =~ /^.+@([^@]+)$/;
	}

	# format TGT expiration time

	my @expirystr;

	if (!%creds && !%tgtcreds) {
		push @expirystr, "credential cache is empty";
		goto do_print;
	}

	my $starting = $init->{start_time} || $tgt->{start_time};

	my $expiry = $init->{expiry_time} || $tgt->{expiry_time};

	my $renew = $tgt ? $tgt->{renew_time} : 0;

	my $flags = $init->{flags} || $tgt->{flags};

	if ($flags and $flags =~ /d/ and $flags =~ /i/ and $starting <= time) {
		push @expirystr, "\e[1;35m"."invalid"."\e[m";
	}

	if ($starting > time) {
		push @expirystr, "postdated, will become valid in ".interval($starting);
	} elsif ($expiry <= time) {
		push @expirystr, "\e[1;31m"."expired"."\e[m";
	} elsif ($expiry <= time+3600) {
		push @expirystr, "\e[1;33m"."expire in ".interval($expiry)."\e[m";
	} else {
		push @expirystr, "expire in ".interval($expiry);
	}

	if ($flags and $flags =~ /R/
	    and $starting <= time and $expiry > time and $renew > time) {
		push @expirystr, "renewable for ".interval($renew);
	} else {
		push @expirystr, "not renewable";
	}

	# output tickets sorted by realm, server name

do_print:

	my $COLOR_TGT_LOCAL	= "32"; # same realm as client
	my $COLOR_TGT_CROSS	= "35"; # immediate cross realm
	my $COLOR_TGT_DISTANT	= "35"; # distant cross realm
	my $COLOR_NO_TGT	= "34"; # tickets with no corresponding TGT
	my $COLOR_REFERRAL	= "34"; # tickets with empty realm

	say "" if $counter++;

	say "Credentials for \e[1m${defprinc}\e[m";
	say "    ", join(", ", @expirystr) if @expirystr;
	say "";

	my @realms = uniq sort {$a eq '' ? 1 :
				$b eq '' ? -1 :
				$a cmp $b} (@extrealms);

	for my $realm ($defrealm, @realms) {
		$tgtcreds{$realm} //= [];
		$creds{$realm} //= [];

		my @creds = (@{$tgtcreds{$realm}},
				sort {mangle_name($a) cmp mangle_name($b)} @{$creds{$realm}});

		my $num_tgt = 0;
		my $num_srv = 0;

		if (!@{$tgtcreds{$realm}}) {
			my ($color, $label);
			if ($realm eq '') {
				$color = $COLOR_REFERRAL;
				$label = "\e[1;${color}m(referral tickets)\e[m";
			} else {
				$color = $COLOR_NO_TGT;
				$label = "\e[1;${color}m${realm}\e[m";
				$label .= " \e[${color}m(no TGT found)\e[m";
			}

			say " "x4, $label;
		}

		for my $tkt (@creds) {
			my $color = "0";
			my $label = "";
			my $width = 0;
			my $istgt = 0;
			my $isinit = 0;

			my ($svname, $svrealm) = $tkt->{server_name} =~ /^(.+)@(.*)$/;

			if ($svname =~ m|^krbtgt/(.+)$| && $svrealm ne '') {
				my $nextrealm = $1;
				$istgt = 1;

				$color = $svrealm eq $defrealm
					? ($svrealm eq $nextrealm
						? $COLOR_TGT_LOCAL
						: $COLOR_TGT_CROSS)
					: $COLOR_TGT_DISTANT;

				$label .= "\e[1;${color}m${nextrealm}\e[m";
				$width += length $nextrealm;

				if ($svrealm eq $nextrealm && $svrealm eq $defrealm) {
					$label .= " \e[${color}m(local)\e[m";
					$width += length " (local)";
				} else {
					$label .= " \e[${color}m(via ${svrealm})\e[m";
					$width += length " (via $svrealm)";
				}
				$num_tgt++;
			} else {
				$label .= " "x2;
				$width += 2;

				$width += length $svname;
				$svname =~ s|/([^.]+)\.|/\e[1m$1\e[m.|;
				$label .= $svname;

				if ($tkt->{client_name} ne $defprinc) {
					my ($defname) = ($tkt->{client_name} =~ /^(.+)@/);
					$width += length " for $defname";
					$label .= " \e[36mfor \e[1m$defname\e[m";
				}
				if ($svrealm eq "") {
					$width += length " (referral)";
					$label .= " \e[34m(referral)\e[m";
				}
				$num_srv++;
			}
			
			my $flags = $tkt->{flags};

			my $expiry = $tkt->{expiry_time} <= time
					? "expired"
					: interval($tkt->{expiry_time});

			my $expiry_c = (
					$tkt->{start_time} > time
					? "\e[1;35m"
					: $tkt->{expiry_time} <= time
					? "\e[1;31m"
					: $tkt->{expiry_time} <= time+300
					? "\e[33m"
					: ""
					). $expiry."\e[m";

			say " "x4,
				$label,
				" "x(45-$width),
				" ",
				$flags,
				" "x(8-length $flags),
				" ",
				" "x(8-length $expiry),
				$expiry_c,
				;
		}

		if (!$num_srv) {
			say " "x4,
				"  \e[2m(no service tickets)\e[m",
				;
		}
	}
	return 0;
}

my %opts;
my $ccache	= undef;
my $list_all	= undef;

getopts('c:lL', \%opts) or usage();
if (exists $opts{c}) {
	$ccache = $opts{c};
	$list_all = 0;
} elsif (exists $opts{l}) {
	$list_all = 1;
} elsif (exists $opts{L}) {
	$list_all = 0;
}

if (defined(my $defcc = $ccache // $ENV{KRB5CCNAME})) {
	$list_all //= ($defcc =~ /^DIR:[^:]/);
}

my @visible;

if ($list_all) {
	@visible = map {$_->[0]} sort {$a->[1] cmp $b->[1]} enum_ccaches();
} else {
	@visible = ($ccache);
}

if (@visible) {
	display_ccache($_) for @visible;
} else {
	say "No credentials cache found.";
}
